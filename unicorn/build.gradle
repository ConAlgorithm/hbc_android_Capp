apply plugin: 'com.android.library'

def getGitVersion() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable ignored) {
        return "";
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 26
        versionName '3.4.0'
        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

project.afterEvaluate {
    tasks.all { task ->
        if (task.name =~ /generate.*BuildConfig/) {
            task.dependsOn(propagateBuildConfig)
        }
    }
}

// workaround of gradle bug: build type of library is always release.
task propagateBuildConfig << {
    project.android.buildTypes.all { type ->
        type.debuggable = rootProject.ext.has("debugBuild") ? rootProject.ext.debugBuild : false
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile fileTree(dir: 'libs', exclude: ['android-support-v4.jar'], include: ['*.jar'])
}